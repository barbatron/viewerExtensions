USE NQViewer
GO

IF (OBJECT_ID('CUSTOMER_QUEUE_INFO_EXT') IS NOT NULL) DROP VIEW CUSTOMER_QUEUE_INFO_EXT
GO

CREATE VIEW CUSTOMER_QUEUE_INFO_EXT AS
SELECT
	a.IINDEX, 

	a.OFFICEGUID,
	a.OFFICE_NR,
	a.OFFICE_NAME,

-- Ticket number
	a.TICKET,
	
-- Name / Text 	
	'' AS [CUSTOMER_TEXT],
	
-- Queue name 
	CASE a.QUEUE_TYPE
		WHEN 1 THEN st.SERVICETYPE_NAME
		WHEN 2 THEN c.CASHIER_NAME
		WHEN 3 THEN u.USER_NAME
		ELSE '' END
	AS [QUEUE_NAME],

-- Time.Start 
	a.TICKET_TIME AS [TIME_START],
-- Time.Served = FORWARDING_TIME
	a.FORWARDING_TIME AS [TIME_SERVED],
-- Time.Ended = END_TIME
	a.END_TIME AS [TIME_ENDED],
-- Time.Waiting = WAITING_TIME
	a.WAITING_TIME AS [TIME_WAITING],
-- Time.Service = SERVICE_TIME
	a.SERVICE_TIME AS [TIME_SERVICE],

-- Time.Lost ticket =  ? finns ej, måste till NQVwr

-- Service.Workstation # = CASHIER_NR
	c.CASHIER_NR AS [SERVICE_WORKSTATION],
-- Service.Workstation name = CASHIER_NAME
	c.CASHIER_NAME AS [SERVICE_WORKSTATION_NAME],
-- Service.User # =    USER_NR
	u.USER_NR AS [SERVICE_USER_NR],
-- Service.User name = USER_NAME
	u.USER_NAME AS [SERVICE_USER_NAME],

-- Feedback.Name
	ss.SUBSERVICE_NAME AS [FEEDBACK_NAME],
-- Feedback.Value
	CASE 
		WHEN ss.SUBSERVICE_NR IS NOT NULL 
		AND ss.SUBSERVICE_NR IN (101,102,103,104,105) 
		THEN ss.SUBSERVICE_NR - 100
		ELSE NULL END AS [FEEDBACK_VALUE]

FROM CUSTOMER_QUEUE_INFO a

-- Attach information about the ST/C/U entities that served the customer:
-- TODO: This won't hold well when the going gets rough; split into 3 views or something?
LEFT OUTER JOIN SERVICETYPE_QUEUE_INFO st ON st.OFFICEGUID = a.OFFICEGUID AND st.SERVICETYPE_NR = a.SERVICETYPE_NR 
LEFT OUTER JOIN CASHIER_QUEUE_INFO c ON c.OFFICEGUID = a.OFFICEGUID AND c.CASHIER_NR = a.CASHIER_NR
LEFT OUTER JOIN USER_QUEUE_INFO u ON u.OFFICEGUID = a.OFFICEGUID AND u.USER_NR = a.USER_NR

LEFT OUTER JOIN SUBSERVICE_QUEUE_INFO ss ON ss.OFFICEGUID = a.OFFICEGUID AND ss.TRACK_NR = a.TRACK_NR
GO

